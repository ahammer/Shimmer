plugins {
    id 'java-library'
    id 'org.jetbrains.kotlin.jvm'
    id 'org.jetbrains.kotlin.plugin.serialization'
    id 'maven-publish'
}

test {
    useJUnitPlatform()
    testLogging {
        outputs.upToDateWhen { false }
        showStandardStreams = true
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    api "org.jetbrains.kotlinx:kotlinx-serialization-json:1.8.0"
    api "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.9.0"
    implementation 'org.jetbrains.kotlin:kotlin-reflect'
    testImplementation project(':shimmer-test')
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.2'
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.9.0"
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.2'
}

java {
    withSourcesJar()
    withJavadocJar()
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifactId = 'shimmer-core'

            versionMapping {
                usage('java-api') { fromResolutionOf('runtimeClasspath') }
                usage('java-runtime') { fromResolutionResult() }
            }

            pom {
                name = 'Shimmer Core'
                description = 'AI-backed proxy library for Kotlin â€” core module with DSL builder, annotations, and resilience.'
                url = 'https://github.com/AdamHammer/Shimmer'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'adamhammer'
                        name = 'Adam Hammer'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/AdamHammer/Shimmer.git'
                    developerConnection = 'scm:git:ssh://github.com/AdamHammer/Shimmer.git'
                    url = 'https://github.com/AdamHammer/Shimmer'
                }
            }
        }
    }
    repositories {
        maven {
            name = 'local'
            url = layout.buildDirectory.dir('repo')
        }
    }
}
